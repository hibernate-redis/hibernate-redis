
description = 'Hibernate 2nd level cache using Redis.'

archivesBaseName = "hibernate-redis"

javadoc {
    destinationDir = file("${buildDir}/apidocs")
    options.encoding = 'UTF-8'
    failOnError = false
}

apply plugin: 'groovy'
apply plugin: 'net.researchgate.release'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'

// make sure the release plugin will uploadArchives before updating the version to the next snapshot
afterReleaseBuild.dependsOn uploadArchives

dependencies {
    compile(group: 'org.hibernate', name: 'hibernate-core', version:hibernateVersion) {
        exclude(module: 'jboss-logging')
    }
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version:hibernateVersion
    compile group: 'redis.clients', name: 'jedis', version:'2.8.1'
    compile group: 'org.xerial.snappy', name: 'snappy-java', version:'1.1.2.1'
    compile group: 'de.ruedigermoeller', name: 'fst', version:'2.45'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.7'

    testCompile group: 'org.hibernate', name: 'hibernate-testing', version:hibernateVersion
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'org.easytesting', name: 'fest-assert', version:'1.4'
    testCompile group: 'com.zaxxer', name: 'HikariCP', version:'2.4.5'
    testCompile group: 'com.h2database', name: 'h2', version:'1.3.175'
    testCompile group: 'org.springframework', name: 'spring-core', version:springVersion
    testCompile group: 'org.springframework', name: 'spring-context', version:springVersion
    testCompile group: 'org.springframework', name: 'spring-beans', version:springVersion
    testCompile group: 'org.springframework', name: 'spring-tx', version:springVersion
    testCompile group: 'org.springframework', name: 'spring-orm', version:springVersion
    testCompile group: 'org.springframework', name: 'spring-test', version:springVersion
    testCompile group: 'org.springframework.data', name: 'spring-data-jpa', version:'1.6.0.RELEASE'
    testCompile(group: 'mysql', name: 'mysql-connector-java', version:'5.1.27')

    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"

    // optional dependencies for using Spock
    testRuntime "cglib:cglib-nodep:3.1"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.1"    // allows mocking of classes without default constructor (together with CGLIB)
}

nexus {
    sign = true
}

modifyPom {
    project {
        name 'hibernate-redis'
        packaging 'jar'
        description 'Hibernate second level cache with Redis'
        url "https://github.com/hibernate-redis/hibernate-redis"
        scm {
            url "https://github.com/hibernate-redis/hibernate-redis"
            connection "scm:git:ssh://git@github.com/hibernate-redis/hibernate-redis.git"
            developerConnection "scm:git:ssh://git@github.com/hibernate-redis/hibernate-redis.git"
        }
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/license/LICENSE-2.0.txt'
            }
        }
        issueManagement {
            system 'github'
            url 'http://github.com/hibernate-redis/hibernate-redis/issues'
        }
        developers {
            developer {
                name 'Jon Palmer'
                email 'jpalmer@nanigans.com'
                organization 'Nanigans'
                organizationUrl 'http://www.nanigans.com'
            }
            developer {
                name 'Johno Crawford'
                email 'johno@sulake.com'
                organization 'Sulake'
                organizationUrl 'http://www.sulake.com'
            }
        }
    }
}

